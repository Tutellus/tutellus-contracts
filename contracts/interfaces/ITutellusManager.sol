// SPDX-License-Identifier: Unlicensed
pragma solidity 0.8.9;

import "@openzeppelin/contracts-upgradeable/access/IAccessControlUpgradeable.sol";

/// @title The interface of TutellusManager
/// @dev Manages smart contracts deployments, ids and protocol roles
interface ITutellusManager is IAccessControlUpgradeable {

    /** EVENTS */

    /// @dev Emitted when a new link is set between id and addr
    /// @param id Hashed identifier linked to proxy
    /// @param proxy Proxy contract address
    /// @param implementation Implementation contract address
    /// @param upgrade Flag: true when the proxy implementation is upgraded
    event Deployment(
        bytes32 indexed id,
        address indexed proxy,
        address indexed implementation,
        bool upgrade
    );

    /// @dev Emitted when an identifier is locked forever to an address
    /// @param id Hashed identifier linked to addr
    /// @param addr Address linked to id
    event Locked(
        bytes32 indexed id,
        address indexed addr
    );

    /// @dev Emitted when a new link is set between id and addr
    /// @param id Hashed identifier linked to addr
    /// @param addr Address linked to id
    event NewId(
        bytes32 indexed id,
        address indexed addr
    );

    /** METHODS */

    /// @dev Deploys / upgrades a proxy contract by deploying a new implementation
    /// @param id Hashed identifier linked to the proxy contract
    /// @param bytecode Bytecode for the new implementation
    /// @param initializeCalldata Calldata for the initialization of the new contract (if necessary)
    /// @return implementation Address of the new implementation
    function deploy(
        bytes32 id,
        bytes memory bytecode,
        bytes memory initializeCalldata
    ) external returns ( address implementation );

    /// @dev Deploys / overwrites a proxy contract with an existing implementation 
    /// @param id Hashed identifier linked to the proxy contract
    /// @param implementation Address of the existing implementation contract
    /// @param initializeCalldata Calldata for the initialization of the new contract (if necessary)
    function deployProxyWithImplementation(
        bytes32 id,
        address implementation,
        bytes memory initializeCalldata
    ) external;

    /// @dev Initializes the manager and sets necessary roles
    function initialize() external;

    /// @dev Locks immutably a link between an address and an id
    /// @param id Hashed identifier linked to the proxy contract
    function lock(
        bytes32 id
    ) external;

    /// @dev Returns whether a hashed identifier is locked or not
    /// @param id Hashed identifier linked to the proxy contract
    /// @return isLocked A boolean: true if locked, false if not
    function locked(
        bytes32 id
    ) external returns ( bool isLocked );

    /// @dev Returns the address linked to a hashed identifier
    /// @param id Hashed identifier
    /// @return addr Address linked to id
    function get(
        bytes32 id
    ) external view returns ( address addr );

    /// @dev Returns the hashed identifier linked to an address
    /// @param addr Address
    /// @return id Hashed identifier linked to addr
    function idOf(
        address addr
    ) external view returns ( bytes32 id );

    /// @dev Sets a link between a hashed identifier and an address
    /// @param id Hashed identifier
    /// @param addr Address
    function setId(
        bytes32 id,
        address addr
    ) external;

    /// @dev Upgrades a proxy contract with an existing implementation 
    /// @param id Hashed identifier linked to the proxy contract
    /// @param implementation Address of the existing implementation contract
    /// @param initializeCalldata Calldata for the initialization of the new contract (if necessary)
    function upgrade(
        bytes32 id,
        address implementation,
        bytes memory initializeCalldata
    ) external;

    /// @dev Returns upgrader role hashed identifier
    /// @return role Hashed string of UPGRADER_ROLE
    function UPGRADER_ROLE() external returns ( bytes32 role );

}